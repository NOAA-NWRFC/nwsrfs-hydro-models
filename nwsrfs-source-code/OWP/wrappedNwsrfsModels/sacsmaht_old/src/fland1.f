C$PRAGMA C (ICP1)
C MEMBER FLAND1
C  (from old member FCFLAND1)
C
      SUBROUTINE FLAND1(PAR,DT,PXV,EDMND,TA,WE,AESC,SH,TCI,
     1  IPRINT,IOUT,IDAY,IHOUR,MONTH,IYEAR,ICALL
     +  ,RUZICE,RLZICE,RUZPERC)
c  DT is in days here
C.......................................
C     THIS SUBROUTINE EXECUTES THE 'SAC-SMA ' OPERATION FOR ONE TIME
C         PERIOD.
C.......................................

CVK  FROZEN GROUND CHANGES
CVK  UZTWC,UZFWC,LZTWC,LZFSC,LZFPC ARE TOTAL WATER STORAGES
CVK  UZTWH,UZFWH,LZTWH,LZFSH,LZFPH ARE UNFROZEN WATER STORAGES

      REAL PAR(20),stx(10)
      REAL LZTWM,LZFSM,LZFPM,LZSK,LZPK,LZTWC,LZFSC,LZFPC,LZTWD
cc      DIMENSION EPDIST(24)
      REAL LZTWH,LZFSH,LZFPH      

      COMMON/FPMFG1/itta,FGPM(15),ivers,ifrze      

CVK_02  NEW COMMON BLOCK FOR INTERPOLATED SOIL TEMP AND SOIL PARAMETERS
      COMMON/TSLINT/TSINT(10),NINT,SWINT(10),SWHINT(10),NINTW
      COMMON/FRDSTFG/SMAX,PSISAT,BRT,SWLT,QUARTZ,STYPE,NUPL,NSAC,
     +               RTUZ,RTLZ,DZUP,DZLOW      
      COMMON/FRZCNST/ FRST_FACT,ZBOT      
      
CVK
CVK  NEW FG VERSION PARAMETERS & SOIL LAYER DEFINITION:
CVK          FGPM(1) - SOIL TEXTURE CLASS
CVK          FGPM(2) - OPTIONAL, SOIL TEMPERATURE AT THE 3M DEPTH
CVK          FGPM(3) - OPTIONAL, POROSITY OF RESIDUE LAYER
CVK          PAR(18) [if no calb=FGPM(4)] - RUNOFF REDUCTION PARAMETER 1
CVK          PAR(19) [if no calb=FGPM(5)] - RUNOFF REDUCTION PARAMETER 2
CVK          PAR(20) [if no calb=FGPM(6)] - RUNOFF REDUCTION PARAMETER 3
CVK          FGPM(6) - RUNOFF REDUCTION PARAMETER 3 (FOR ERIC'S VERSION ONLY)
CVK          FGPM(7) - NUMBER OF SOIL LAYERS 
CVK          FGPM(8)-FGPM(15) - DEPTHS OF SOIL LAYERS (M), NEGATIVE.
CVK                             FIRST LAYER (RESIDUE) DEPTH=-0.03M

      COMMON/FSMCO1/UZTWC,UZFWC,LZTWC,LZFSC,LZFPC,ADIMC,FGCO(6),RSUM(7),
     1   PPE,PSC,PTA,PWE,PSH,TSOIL(8),smc(8),sh2o(8)
cv07     +               ppx,ppsc,pta,pwe,psh,tsoil(8)          

      COMMON/FSUMS1/SROT,SIMPVT,SRODT,SROST,SINTFT,SGWFP,SGWFS,SRECHT,
     1              SETT,SE1,SE3,SE4,SE5
cv07 Previous states
      REAL LZTWC0,LZFSC0,LZFPC0
      COMMON/PRV_STATES/ UZTWC0,UZFWC0,LZTWC0,LZFSC0,LZFPC0
      
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /APN/ofs/src/fcst_sac/RCS/fland1.f,v $
     . $',                                                             '
     .$Id: fland1.f,v 1.2 1995/11/14 $
     . $' /
C    ===================================================================

CVK  ADDITION FOR FROZEN DEPTH ESTIMATION
      SAVE IIPREV,FRSTPREV,FRZPREV
      IF(FGCO(1) .EQ. 0.) THEN
       IIPREV=0
       FRSTPREV=0.
       FRZPREV=0.
      ENDIF
CVK--------------------------------------
cv07  Remember previous SAC states
      UZTWC0 = UZTWC
      UZFWC0 = UZFWC
      LZTWC0 = LZTWC
      LZFSC0 = LZFSC
      LZFPC0 = LZFPC
      
c define parameters from the array
      uztwm=par(1)
      uzfwm=par(2)
      uzk=par(3)
      pctim=par(4)
      adimp=par(5)
      riva=par(6)
      zperc=par(7)
      rexp=par(8)
      lztwm=par(9)
      lzfsm=par(10)
      lzfpm=par(11)
      lzsk=par(12)
      lzpk=par(13)
      pfree=par(14)
      rserv=par(15)
      side=par(16)
      efc=par(17)
c actually PAR(18)-PAR(20) == FGPM(4)-FGPM(6)
      saved=par(15)*(par(11)+par(10))
      parea=1.0-par(4)-par(5)
       
      IF(IVERS .LE. 1) THEN
CVK  OLD FROZEN GROUND VERSION: KEEP UNFROZEN WATER = TOTAL
       RUZICE=UZK
       RLZICE=LZSK
       RUZPERC=1.0
       UZTWH=UZTWC
       UZFWH=UZFWC
       LZTWH=LZTWC
       LZFSH=LZFSC
       LZFPH=LZFPC
      ELSE        
CVK  NEW FROZEN GROUND VERSION: USE ESTIMATED UNFROZEN WATER
CVK  REDEFINE UNFROZEN WATER VARIABLES IF THEY ARE NEGATIVE
C       
       UZTWH=FGCO(2)
       IF(UZTWH .LT. 0.) UZTWH=0.
       UZFWH=FGCO(3)
       IF(UZFWH .LT. 0.) UZFWH=0.
       LZTWH=FGCO(4)
       IF(LZTWH .LT. 0.) LZTWH=0.
       LZFSH=FGCO(5)
       IF(LZFSH .LT. 0.) LZFSH=0.
       LZFPH=FGCO(6)
       IF(LZFPH .LT. 0.) LZFPH=0.
CVK  RUZICE & RLZICE ARE REDUCTION OF FREE WATER MOVEMENT 
CVK  BASED ON KULIK'S THEORY: Kfrz = Kunfrz/(1+FGPM(4)*ICE)**2 
       RUZICE=0.001*RTUZ*(UZTWC-FGCO(2)+UZFWC-FGCO(3))/DZUP
       RLZICE=0.001*RTLZ*(LZTWC-FGCO(4)+LZFSC-FGCO(5))/DZLOW
       RUZPERC=1.0
       IF(RUZICE .EQ. 0.) THEN
        RUZICE = UZK
       ELSE 
        RUZPERC=1.0/((1.+FGPM(4)*RUZICE)**2)
        RUZICE=1.-EXP(LOG(1.-UZK)*RUZPERC)
       ENDIF
       IF(RLZICE .EQ. 0.) THEN
        RLZICE = LZSK
       ELSE  
        RLZICE=1.0/((1.+FGPM(4)*RLZICE)**2)
        RLZICE=1.-EXP(LOG(1.-LZSK)*RLZICE)
       ENDIF 
      ENDIF
C.......................................
C     COMPUTE EVAPOTRANSPIRATION LOSS FOR THE TIME INTERVAL.
C        EDMND IS THE ET-DEMAND FOR THE TIME INTERVAL
cc      EDMND=EP*EPDIST(KINT)
cVK ADJUST EDMND FOR EFFECT OF SNOW & FOREST COVER.
c from EX1 OFS subroutine
      EDMND=EFC*EDMND+(1.0-EFC)*(1.0-AESC)*EDMND
C
C     COMPUTE ET FROM UPPER ZONE.
CVK      E1=EDMND*(UZTWC/UZTWM)
CVK  ONLY UNFROZEN WATER CAN BE EVAPORATED
      E1=EDMND*(UZTWH/UZTWM)

      RED=EDMND-E1
C     RED IS RESIDUAL EVAP DEMAND
CVK      UZTWC=UZTWC-E1
      UZTWH=UZTWH-E1

      E2=0.0
CV.K      IF(UZTWC.GE.0.) THEN
      IF(UZTWH.GE.0.) THEN
CV.K    SUBTRACT ET FROM TOTAL WATER STORAGE
       UZTWC=UZTWC-E1
       GO TO 220
      ENDIF 

C     E1 CAN NOT EXCEED UZTWC
      E1=E1+UZTWH
      UZTWH=0.0
CV.K   REDUCE TOTAL TENSION WATER BY ACTUAL E1
      UZTWC=UZTWC-E1
            
      RED=EDMND-E1
      IF(UZFWH.GE.RED) GO TO 221

C     E2 IS EVAP FROM UZFWC.
      E2=UZFWH
      UZFWH=0.0
CV.K   REDUCE TOTAL FREE WATER BY ACTUAL E2
      UZFWC=UZFWC-E2
            
      RED=RED-E2
      GO TO 225
  221 E2=RED
CVK   SUBTRACT E2 FROM TOTAL & UNFROZEN FREE WATER STORAGES
      UZFWC=UZFWC-E2
      UZFWH=UZFWH-E2
      RED=0.0
  220 IF((UZTWC/UZTWM).GE.(UZFWC/UZFWM)) GO TO 225
C     UPPER ZONE FREE WATER RATIO EXCEEDS UPPER ZONE
C     TENSION WATER RATIO, THUS TRANSFER FREE WATER TO TENSION
      UZRAT=(UZTWC+UZFWC)/(UZTWM+UZFWM)

CV.K  ACCOUNT FOR RATIO OF UNFROZEN WATER ONLY
CV.K  AND ADJUST FOUR SOIL STATES 
CV.K      UZTWC=UZTWM*UZRAT
CV.K      UZFWC=UZFWM*UZRAT
      DUZTWC=UZTWM*UZRAT-UZTWC
      IF(DUZTWC .GT. UZFWH) DUZTWC=UZFWH
CV.K  TRANSFERED WATER CAN NOT EXCEED UNFROZEN FREE WATER
      UZTWC=UZTWC+DUZTWC
      UZTWH=UZTWH+DUZTWC
      UZFWC=UZFWC-DUZTWC
      UZFWH=UZFWH-DUZTWC
            
CV.K  CHECK UNFROZEN WATER STORAGES TOO
  225 IF (UZTWC.LT.0.00001) THEN
       UZTWC=0.0
       UZTWH=0.0
      ENDIF 
      IF (UZFWC.LT.0.00001) THEN
       UZFWC=0.0
       UZFWH=0.0
      ENDIF 
C
C     COMPUTE ET FROM THE LOWER ZONE.
C     COMPUTE ET FROM LZTWC (E3)
CV.K  ONLY UNFROZEN WATER CAN BE EVAPORATED
      E3=RED*(LZTWH/(UZTWM+LZTWM))
      LZTWH=LZTWH-E3
      IF(LZTWH.GE.0.0) THEN
       LZTWC=LZTWC-E3
       GO TO 226
      ENDIF
       
C     E3 CAN NOT EXCEED LZTWC
      E3=E3+LZTWH
      LZTWH=0.0
CV.K   REDUCE TOTAL TENSION WATER BY E3
       LZTWC=LZTWC-E3
             
  226 RATLZT=LZTWC/LZTWM
      RATLZ=(LZTWC+LZFPC+LZFSC-SAVED)/(LZTWM+LZFPM+LZFSM-SAVED)
      IF(RATLZT.GE.RATLZ) GO TO 230
C     RESUPPLY LOWER ZONE TENSION WATER FROM LOWER
C     ZONE FREE WATER IF MORE WATER AVAILABLE THERE.
      DEL=(RATLZ-RATLZT)*LZTWM
CV.K  ONLY UNFROZEN WATER CAN BE TRANSFERED
      SFH=LZFSH+LZFPH
      IF(DEL .GT. SFH) DEL=SFH
      LZFSH=LZFSH-DEL
      IF(LZFSH .GE. 0.0) THEN
C     TRANSFER FROM LZFSC TO LZTWC.      
       LZFSC=LZFSC-DEL
      ELSE
C     IF TRANSFER EXCEEDS LZFSC THEN REMAINDER COMES FROM LZFPC
       LZFPC=LZFPC+LZFSH
       LZFPH=LZFPH+LZFSH
       LZFSC=LZFSC-(LZFSH+DEL)
       LZFSH=0.0
      ENDIF
      LZTWC=LZTWC+DEL
      LZTWH=LZTWH+DEL

CV.K  CHECK UNFROZEN WATER STORAGE
  230 IF (LZTWC.LT.0.00001) THEN
       LZTWC=0.0
       LZTWH=0.0 
      ENDIF 
C
C     COMPUTE ET FROM ADIMP AREA.-E5
      E5=E1+(RED+E2)*((ADIMC-E1-UZTWC)/(UZTWM+LZTWM))
C      ADJUST ADIMC,ADDITIONAL IMPERVIOUS AREA STORAGE, FOR EVAPORATION.
      ADIMC=ADIMC-E5
      IF(ADIMC.GE.0.0) GO TO 231
C     E5 CAN NOT EXCEED ADIMC.
      E5=E5+ADIMC
      ADIMC=0.0
  231 E5=E5*ADIMP
C     E5 IS ET FROM THE AREA ADIMP.
C.......................................
C     COMPUTE PERCOLATION AND RUNOFF AMOUNTS.
      TWX=PXV+UZTWC-UZTWM       
C     TWX IS THE TIME INTERVAL AVAILABLE MOISTURE IN EXCESS
C     OF UZTW REQUIREMENTS.
      IF(TWX.GE.0.0) GO TO 232
C     ALL MOISTURE HELD IN UZTW--NO EXCESS.
      UZTWC=UZTWC+PXV
CV.K  ADJUST UNFROZEN TENSION WATER
      UZTWH=UZTWH+PXV      

      TWX=0.0
      GO TO 233
C      MOISTURE AVAILABLE IN EXCESS OF UZTWC STORAGE.
CV.K  232 UZTWC=UZTWM
  232 UZTWH=UZTWH+(UZTWM-UZTWC)
      UZTWC=UZTWM
      
  233 ADIMC=ADIMC+PXV-TWX
C
C     COMPUTE IMPERVIOUS AREA RUNOFF.
      ROIMP=PXV*PCTIM
C      ROIMP IS RUNOFF FROM THE MINIMUM IMPERVIOUS AREA.
      SIMPVT=SIMPVT+ROIMP
C
C     INITIALIZE TIME INTERVAL SUMS.
      SBF=0.0
      SSUR=0.0
      SIF=0.0
      SPERC=0.0
      SDRO=0.0
      SPBF=0.0
C
C     DETERMINE COMPUTATIONAL TIME INCREMENTS FOR THE BASIC TIME
C     INTERVAL
CV.K      NINC=1.0+0.2*(UZFWC+TWX)
CV.K  PERCOLATE UNFROZEN WATER ONLY
      NINC=1.0+0.2*(UZFWH+TWX)
C     NINC=NUMBER OF TIME INCREMENTS THAT THE TIME INTERVAL
C     IS DIVIDED INTO FOR FURTHER
C     SOIL-MOISTURE ACCOUNTING.  NO ONE INCREMENT
C     WILL EXCEED 5.0 MILLIMETERS OF UZFWC+PAV
      DINC=(1.0/NINC)*DT
C     DINC=LENGTH OF EACH INCREMENT IN DAYS.
      PINC=TWX/NINC
C     PINC=AMOUNT OF AVAILABLE MOISTURE FOR EACH INCREMENT.
C      COMPUTE FREE WATER DEPLETION FRACTIONS FOR
C     THE TIME INCREMENT BEING USED-BASIC DEPLETIONS
C      ARE FOR ONE DAY
CVK INTRODUCED REDUCTION (RUZICE & RLZICE) DUE FROZEN GROUND
CVK HOWEVER, PRIMARY RUNOFF IS UNCHANGED
CVK      DUZ=1.0-((1.0-UZK)**DINC)
CVK      DLZS=1.0-((1.0-LZSK)**DINC)
      DLZP=1.0-((1.0-LZPK)**DINC)
CVK  Linear transformation for frozen ground
cc      DUZ=1.0-((1.0-UZK*RUZICE)**DINC)
cc      DLZS=1.0-((1.0-LZSK*RLZICE)**DINC)
CVK  Non-linear (correct) transformation for frozen ground      
      DUZ=1.0-((1.0-RUZICE)**DINC)
      DLZS=1.0-((1.0-RLZICE)**DINC)
C
C CVK  ADJUSTMENT TO DEPLETIONS DUE TO FROZEN WATER
         
C.......................................
C     START INCREMENTAL DO LOOP FOR THE TIME INTERVAL.
      DO 240 I=1,NINC
      ADSUR=0.0
C     COMPUTE DIRECT RUNOFF (FROM ADIMP AREA).
      RATIO=(ADIMC-UZTWC)/LZTWM
      IF (RATIO.LT.0.0) RATIO=0.0
      ADDRO=PINC*(RATIO**2)
C     ADDRO IS THE AMOUNT OF DIRECT RUNOFF FROM THE AREA ADIMP.
C
C     COMPUTE BASEFLOW AND KEEP TRACK OF TIME INTERVAL SUM.
CV.K      BF=LZFPC*DLZP
CV.K      LZFPC=LZFPC-BF
CV.K      IF (LZFPC.GT.0.0001) GO TO 234
CV.K      BF=BF+LZFPC
CV.K      LZFPC=0.0
CV.K  BASEFLOW FROM UNFROZEN WATER ONLY   
      BF=LZFPH*DLZP
      LZFPH=LZFPH-BF
      IF (LZFPH.GT.0.0001) THEN
       LZFPC=LZFPC-BF
       GO TO 234
      ENDIF
      BF=BF+LZFPH
      LZFPH=0.0
      LZFPC=LZFPC-BF
      IF(LZFPC .LE. 0.0001) LZFPC=0.0
CV.K-------------------------------------
C      
  234 SBF=SBF+BF
      SPBF=SPBF+BF
CV.K  SUPPLAMENTAL FLOW FROM UNFROZEN WATER ONLY (NOTE, DLZS
CV.K  NOTE, DLZS IS REDUCED DUE FROZEN GROUND
CV.K      BF=LZFSC*DLZS
CV.K      LZFSC=LZFSC-BF
CV.K      IF(LZFSC.GT.0.0001) GO TO 235
CV.K      BF=BF+LZFSC
CV.K      LZFSC=0.0
      BF=LZFSH*DLZS
      LZFSH=LZFSH-BF
      IF(LZFSH.GT.0.0001) THEN
       LZFSC=LZFSC-BF
       GO TO 235
      ENDIF
      BF=BF+LZFSH
      LZFSH=0.0
      LZFSC=LZFSC-BF
      IF(LZFSC .LE. 0.0001) LZFSC=0.0
CV.K--------------------------------------------
C       
  235 SBF=SBF+BF
C
C      COMPUTE PERCOLATION-IF NO WATER AVAILABLE THEN SKIP
ccvk      IF((PINC+UZFWC).GT.0.01) GO TO 251
      xx1=PINC+UZFWH
      IF(xx1.GT.0.01) GO TO 251
      UZFWC=UZFWC+PINC
CV.K  ADD TO UNFROZEN WATER ALSO
      UZFWH=UZFWH+PINC
      GO TO 249
  251 PERCM=LZFPM*DLZP+LZFSM*DLZS
CVK      PERC=PERCM*(UZFWC/UZFWM)
CV.K  USE ONLY UNFROZEN WATER RATIOS 
ccvk  new change: PERCOLATION REDUCED BY RUZPERC 
CC       PERC=PERCM*(UZFWH/UZFWM)*RUZICE
      PERC=PERCM*(UZFWH/UZFWM)*RUZPERC
      
CV.K      DEFR=1.0-((LZTWC+LZFPC+LZFSC)/(LZTWM+LZFPM+LZFSM))
cvk 6/22/00      DEFR=1.0-((LZTWH+LZFPH+LZFSH)/(LZTWM+LZFPM+LZFSM))
cvk  better to keep original definition of DEFR using total water
      DEFR=1.0-((LZTWC+LZFPC+LZFSC)/(LZTWM+LZFPM+LZFSM))
      
C     DEFR IS THE LOWER ZONE MOISTURE DEFICIENCY RATIO
      FR=1.0
C     FR IS THE CHANGE IN PERCOLATION WITHDRAWAL DUE TO FROZEN GROUND.
      FI=1.0
C     FI IS THE CHANGE IN INTERFLOW WITHDRAWAL DUE TO FROZEN GROUND.
      IF (IFRZE.EQ.0) GO TO 239
       UZDEFR=1.0-((UZTWC+UZFWC)/(UZTWM+UZFWM))
CVK
CVK     CALL FGFR1(DEFR,FR,UZDEFR,FI)
CVK      IF( IVERS .EQ. 1) THEN
CVK  IF IVERS=1, OLD VERSION; IF IVERS=2, NEW VERS. FROST INDEX,
CVK  BUT OLD VERS. OF PERCOLAT. AND INTERFLOW REDUCTION
      IF( IVERS .LE. 2) CALL FGFR1(DEFR,FR,UZDEFR,FI)
      
      IF(IVERS .EQ. 3 .AND. FGPM(5) .GT. 0.) THEN
CVK  OPTIONAL VERSION TO ACCOUNT FOR ADDITIONAL IMPERVIOUS
CVK  AREAS EFFECTS DUE FROZEN GROUND
       FR=1-SURFRZ1(FGCO(1),FGPM(6),FGPM(5))
       FI=FR
      ENDIF 
      
  239 PERC=PERC*(1.0+ZPERC*(DEFR**REXP))*FR
C     NOTE...PERCOLATION OCCURS FROM UZFWC BEFORE PAV IS ADDED.
CV.K      IF(PERC.LT.UZFWC) GO TO 241
      IF(PERC.LT.UZFWH) GO TO 241
C      PERCOLATION RATE EXCEEDS UZFWH.
CV.K      PERC=UZFWC
      PERC=UZFWH
C     PERCOLATION RATE IS LESS THAN UZFWH.
  241 UZFWC=UZFWC-PERC
CV.K  ADJUST UNFROZEN STORAGE ALSO  
      UZFWH=UZFWH-PERC    

C     CHECK TO SEE IF PERCOLATION EXCEEDS LOWER ZONE DEFICIENCY.
      CHECK=LZTWC+LZFPC+LZFSC+PERC-LZTWM-LZFPM-LZFSM
      IF(CHECK.LE.0.0) GO TO 242
      PERC=PERC-CHECK
      UZFWC=UZFWC+CHECK
CV.K  ADJUST UNFROZEN STARAGE ALSO
      UZFWH=UZFWH+CHECK        

  242 SPERC=SPERC+PERC
C     SPERC IS THE TIME INTERVAL SUMMATION OF PERC
C
C     COMPUTE INTERFLOW AND KEEP TRACK OF TIME INTERVAL SUM.
C     NOTE...PINC HAS NOT YET BEEN ADDED
CV.K      DEL=UZFWC*DUZ*FI
CVK  INTERFLOW ALSO REDUCED DUE FROFEN GROUND (DUZ REDUCED BY RUZICE)
CVK  ADDITIONAL REDUCTION DUE IMPERVIOUS FROZEN AREAS (FI) IS OPTIONAL
CVK  IN THE NEW VERSION. BASIC OPTION IS FI=1
      DEL=UZFWH*DUZ*FI
      SIF=SIF+DEL
      UZFWC=UZFWC-DEL
CV.K  ADJUST UNFROZEN STORAGE ALSO
      UZFWH=UZFWH-DEL      

C     DISTRIBE PERCOLATED WATER INTO THE LOWER ZONES
C     TENSION WATER MUST BE FILLED FIRST EXCEPT FOR THE PFREE AREA.
C     PERCT IS PERCOLATION TO TENSION WATER AND PERCF IS PERCOLATION
C         GOING TO FREE WATER.
      PERCT=PERC*(1.0-PFREE)
      xx1=PERCT+LZTWC
      IF (xx1.GT.LZTWM) GO TO 243
      LZTWC=LZTWC+PERCT
CV.K  ADJUST UNFROZEN STORAGE ALSO
      LZTWH=LZTWH+PERCT      
      PERCF=0.0
      GO TO 244
  243 PERCF=PERCT+LZTWC-LZTWM
CV.K  CHANGE UNFROZEN WATER STORAGE
      LZTWH=LZTWH+LZTWM-LZTWC  
      LZTWC=LZTWM
C
C      DISTRIBUTE PERCOLATION IN EXCESS OF TENSION
C      REQUIREMENTS AMONG THE FREE WATER STORAGES.
  244 PERCF=PERCF+PERC*PFREE
      IF(PERCF.EQ.0.0) GO TO 245
      HPL=LZFPM/(LZFPM+LZFSM)
C     HPL IS THE RELATIVE SIZE OF THE PRIMARY STORAGE
C     AS COMPARED WITH TOTAL LOWER ZONE FREE WATER STORAGE.
      RATLP=LZFPC/LZFPM
      RATLS=LZFSC/LZFSM
C     RATLP AND RATLS ARE CONTENT TO CAPACITY RATIOS, OR
C     IN OTHER WORDS, THE RELATIVE FULLNESS OF EACH STORAGE
      FRACP=(HPL*2.0*(1.0-RATLP))/((1.0-RATLP)+(1.0-RATLS))
C     FRACP IS THE FRACTION GOING TO PRIMARY.
      IF (FRACP.GT.1.0) FRACP=1.0
      PERCP=PERCF*FRACP
      PERCS=PERCF-PERCP
C     PERCP AND PERCS ARE THE AMOUNT OF THE EXCESS
C     PERCOLATION GOING TO PRIMARY AND SUPPLEMENTAL
C      STORGES,RESPECTIVELY.
      LZFSC=LZFSC+PERCS
CV.K      IF(LZFSC.LE.LZFSM) GO TO 246
      IF(LZFSC.LE.LZFSM) THEN
       LZFSH=LZFSH+PERCS
       GO TO 246
      ENDIF
       
      PERCS=PERCS-LZFSC+LZFSM
CV.K  ADJUST UNFROZEN STORAGE ALSO
      LZFSH=LZFSH+PERCS
            
      LZFSC=LZFSM
  246 LZFPC=LZFPC+(PERCF-PERCS)
C     CHECK TO MAKE SURE LZFPC DOES NOT EXCEED LZFPM.
CV.K      IF (LZFPC.LE.LZFPM) GO TO 245
      IF (LZFPC.LE.LZFPM) THEN
       LZFPH=LZFPH+(PERCF-PERCS)
       GO TO 245
      ENDIF
       
      EXCESS=LZFPC-LZFPM
      LZTWC=LZTWC+EXCESS
CV.K  ADJUST UNFROZEN STORAGES ALSO
      LZTWH=LZTWH+EXCESS
      LZFPH=LZFPH+(PERCF-PERCS)-EXCESS
      LZFPC=LZFPM
C
C     DISTRIBUTE PINC BETWEEN UZFWC AND SURFACE RUNOFF.
  245 IF(PINC.EQ.0.0) GO TO 249
C     CHECK IF PINC EXCEEDS UZFWM
      xx1=PINC+UZFWC
      IF(xx1.GT.UZFWM) GO TO 248
C     NO SURFACE RUNOFF
      UZFWC=UZFWC+PINC
CV.K  ADJUST UNFROZEN STORAGE ALSO
      UZFWH=UZFWH+PINC
      GO TO 249
C
C     COMPUTE SURFACE RUNOFF (SUR) AND KEEP TRACK OF TIME INTERVAL SUM.
  248 SUR=PINC+UZFWC-UZFWM
      UZFWC=UZFWM
CV.K  ADJUST UNFROZEN STORAGE ALSO
      UZFWH=UZFWH+PINC-SUR
      SSUR=SSUR+SUR*PAREA
      ADSUR=SUR*(1.0-ADDRO/PINC)
C     ADSUR IS THE AMOUNT OF SURFACE RUNOFF WHICH COMES
C     FROM THAT PORTION OF ADIMP WHICH IS NOT
C     CURRENTLY GENERATING DIRECT RUNOFF.  ADDRO/PINC
C     IS THE FRACTION OF ADIMP CURRENTLY GENERATING
C     DIRECT RUNOFF.
      SSUR=SSUR+ADSUR*ADIMP
C
C     ADIMP AREA WATER BALANCE -- SDRO IS THE 6 HR SUM OF
C          DIRECT RUNOFF.
  249 ADIMC=ADIMC+PINC-ADDRO-ADSUR  
      xx1=UZTWM+LZTWM
      IF (ADIMC.LE.xx1) GO TO 247
      ADDRO=ADDRO+ADIMC-xx1
      ADIMC=xx1
  247 SDRO=SDRO+ADDRO*ADIMP
      IF (ADIMC.LT.0.00001) ADIMC=0.0
  240 CONTINUE

C.......................................
C     END OF INCREMENTAL DO LOOP.
C.......................................

C     COMPUTE SUMS AND ADJUST RUNOFF AMOUNTS BY THE AREA OVER
C     WHICH THEY ARE GENERATED.
      EUSED=E1+E2+E3
C     EUSED IS THE ET FROM PAREA WHICH IS 1.0-ADIMP-PCTIM
      SIF=SIF*PAREA
C
C     SEPARATE CHANNEL COMPONENT OF BASEFLOW
C     FROM THE NON-CHANNEL COMPONENT
      TBF=SBF*PAREA
C     TBF IS TOTAL BASEFLOW
      BFCC=TBF*(1.0/(1.0+SIDE))
C     BFCC IS BASEFLOW, CHANNEL COMPONENT
      BFP=SPBF*PAREA/(1.0+SIDE)
      BFS=BFCC-BFP
      IF(BFS.LT.0.0)BFS=0.0
      BFNCC=TBF-BFCC
C     BFNCC IS BASEFLOW,NON-CHANNEL COMPONENT
C
C     ADD TO MONTHLY SUMS.
      SINTFT=SINTFT+SIF
      SGWFP=SGWFP+BFP
      SGWFS=SGWFS+BFS
      SRECHT=SRECHT+BFNCC
      SROST=SROST+SSUR
      SRODT=SRODT+SDRO
C
C     COMPUTE TOTAL CHANNEL INFLOW FOR THE TIME INTERVAL.
      TCI=ROIMP+SDRO+SSUR+SIF+BFCC
C
C     COMPUTE E4-ET FROM RIPARIAN VEGETATION.
      E4=(EDMND-EUSED)*RIVA
C
C     SUBTRACT E4 FROM CHANNEL INFLOW
      TCI=TCI-E4
      IF(TCI.GE.0.0) GO TO 250
      E4=E4+TCI
      TCI=0.0
  250 SROT=SROT+TCI
C
C     COMPUTE TOTAL EVAPOTRANSPIRATION-TET
      EUSED=EUSED*PAREA
      TET=EUSED+E5+E4
      SETT=SETT+TET
      SE1=SE1+E1*PAREA
      SE3=SE3+E3*PAREA
      SE4=SE4+E4
      SE5=SE5+E5
C     CHECK THAT ADIMC.GE.UZTWC
      IF (ADIMC.LT.UZTWC) ADIMC=UZTWC
C
C     COMPUTE NEW FROST INDEX AND MOISTURE TRANSFER.
CVK
CVK  ERIC'S VERSION OF FROST INDEX
      IF(IFRZE .GT. 0) THEN      
       IF (IVERS.EQ.1) CALL FROST1(PXV,SSUR,SDRO,TA,WE,AESC,DT,
     1 IPRINT,icall)
CVK  NEW VERSION OF FROST INDEX  ------------------------------
       IF (IVERS .GT. 1) THEN
        IF(FGCO(2) .LT. 0.) THEN
         FGCO(2)=FGCO(2)+UZTWH
        ELSE
         FGCO(2)=UZTWH
        ENDIF
        IF(FGCO(3) .LT. 0.) THEN
         FGCO(3)=FGCO(3)+UZFWH
        ELSE
         FGCO(3)=UZFWH
        ENDIF
        IF(FGCO(4) .LT. 0.) THEN
         FGCO(4)=FGCO(4)+LZTWH
        ELSE
         FGCO(4)=LZTWH
        ENDIF
        IF(FGCO(5) .LT. 0.) THEN
         FGCO(5)=FGCO(5)+LZFSH
        ELSE
         FGCO(5)=LZFSH
        ENDIF
        IF(FGCO(6) .LT. 0.) THEN
         FGCO(6)=FGCO(6)+LZFPH
        ELSE
         FGCO(6)=LZFPH
        ENDIF

CC        CALL FROST2_1(PXV,TA,LWE,WE,ISC,AESC,SH,DT)
        CALL FROST2_1(PXV,TA,WE,AESC,SH,DT,iprint,iout,icall)
             
       ENDIF
      ENDIF      
C.......................................
C     PRINT DETAILED ACCOUNTING VALUES IF REQUESTED.
      IF (IPRINT.EQ.1) THEN
CVK  OLD FROZEN GROUND VERSION
       IF(IVERS .LE. 1) THEN

        WRITE (IOUT,977) iyear,month,IDAY,IHOUR,UZTWC,UZFWC,LZTWC,
     1  LZFSC,LZFPC,ADIMC,SPERC,ROIMP,SDRO,SSUR,SIF,BFS,BFP,TCI,EDMND,
     2  TET,PXV,uztwh,uzfwh,lztwh,lzfsh,lzfph,we,sh,aesc,ta,fgco(1)

       ELSE
CVK  NEW FROZEN GROUND VERSION
C    CALCULATE FROST DEPTH FOR TEST PURPOSES ONLY, CM
        DO II=FGPM(7),1,-1
         IF(TSOIL(II) .LE. 0.) GOTO 6543
        ENDDO
        FRZD=0.
        if(fgco(1) .lt. 0.) frzd=tsoil(8)
        GOTO 6542
6543    IF(II .EQ. 1) THEN
         DZ1=-0.5*FGPM(8)
        ELSE
         DZ1=0.5*(FGPM(7+II-1)-FGPM(7+II))
        ENDIF
        IF(TSOIL(II) .EQ. 0.) THEN
         FRZD=-(DZ1+FGPM(7+II))*100.
        ELSE 
         DZ2=FGPM(7+II)-FGPM(7+II+1)
         IF(II .NE. FGPM(7)) DZ2=0.5*DZ2
         FRZD=(-TSOIL(II)*(DZ1+DZ2)/(TSOIL(II+1)-TSOIL(II))-
     +            (DZ1+FGPM(7+II)))*100.
        ENDIF
6542    CONTINUE
        IF(II .LT. IIPREV .AND. FGCO(1) .LE. FRSTPREV)
     +    FRZD=FRZPREV
        IIPREV=II
        FRSTPREV=FGCO(1)
        FRZPREV=FRZD        
CVK------------------------------------------------------------
c Generate daily output for defined period

        WRITE (IOUT,905) iyear,month,IDAY,IHOUR,UZTWC,UZFWC,
     1  LZTWC,LZFSC,LZFPC,ADIMC,SPERC,ROIMP,SDRO,SSUR,SIF,BFS,BFP,TCI,
     2  EDMND,TET,PXV,(FGCO(II),II=2,6),WE,SH,AESC,TA,FGCO(1),
CVK_02  CHANGE TO PRINT DESIRED SOIL DEPTH TEMP. INSTEAD OF MODEL LAYERS
     3  (TSOIL(II),II=1,FGPM(7)),TSOIL(7),TSOIL(8),FRZD,
cct     3  (TSINT(II),II=1,NINT),TSOIL(7),TSOIL(8),FRZD,
     4  (SWINT(II),II=1,NINTW),(SWHINT(II),II=1,NINTW)

       ENDIF
  905 FORMAT (1H ,i5,3I3,F7.2,F7.3,F7.2,F7.3,2F7.2,7F7.3,2F8.3,F7.3,
     +        F9.4,5f7.2,2f6.1,F6.3,F9.4,f7.2,8f7.2,20F7.1)
  977 FORMAT (1H ,i5,3I3,F7.2,F7.3,F7.2,F7.3,2F7.2,7F7.3,2F8.3,F7.3,
     +        F9.4,5F7.2,2f6.1,F6.3,F9.4,8f7.2,20F7.1)
      ENDIF

      RETURN
      END
