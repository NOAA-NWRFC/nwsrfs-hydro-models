C  SUBROUTINE RECALCULATES SOIL MOISTURE STATES INTO SAC-SMA STORAGES
C
      SUBROUTINE FST2SAC1(ZSOIL,SMC,SH2O,NUP,NLW,SWLT,RT,TWH,FWH,
     +                    F1WH,TWC,FWC,F1WC,FROST,PX,iout,iexit)
      
      REAL ZSOIL(*),SMC(*),SH2O(*)

cc      COMMON/IONUM/IN,IPR,IPU      

cc      iexit=0
      FROST=0.         
      STOT=0.
      SLIQ=0.
      SX=0.
      SWH=TWH+FWH+F1WH+1000.*SWLT*(ZSOIL(NUP-1)-ZSOIL(NLW))/RT
      DO I=NUP,NLW
       DZ=ZSOIL(I-1)-ZSOIL(I)
       SX=SX+1000.*SH2O(I)*DZ/RT
       FROST=FROST+1000.*(SMC(I)-SH2O(I))*DZ/RT
       STOT=STOT+1000.*(SMC(I)-SWLT)*DZ/RT
       SLIQ=SLIQ+1000.*(SH2O(I)-SWLT)*DZ/RT
      ENDDO
     
C  DH2O IS LIQUID WATER CHANGE DUE TO FREEZING/THAWING
      DH2O=SX-SWH
      if(frost .eq. 0. .and. twh. eq. twc .and. fwh .eq. fwc
     +                      .and. f1wh.eq.f1wc) then
       continue
      else 
       IF(FROST .EQ. 0. .AND. DH2O .EQ. 0.) THEN
C  NO FROZEN GROUND, NO LIQUID WATER CHANGE
        TWH=TWC
        FWH=FWC
        F1WH=F1WC
       ELSE 
C  CHANGE LIQUID WATER STORAGES
        SWH=TWH+FWH
        IF(DH2O .LT. 0.) THEN
C  UNFROZEN WATER REDUCTION (FREEZING)       
         IF(TWH .GE. 0.) THEN
          DSH=SWH+DH2O
          IF(DSH .GE. 0.) THEN
           IF(SWH .GT. 1E-04) THEN
c  new
            if(px .gt. 0. .and. sliq .lt. stot) then
             fwh=fwh+dh2o
             if(fwh .lt. 0.) then
              twh=twh+fwh
              fwh=0.
             endif
            else  
             ALP=DH2O*TWH/SWH
             TWH=TWH+ALP
             FWH=FWH+DH2O-ALP
            endif
           ELSE
            TWH=TWH+DH2O
           ENDIF
          ELSE
           TWH=DSH
           FWH=0.
          ENDIF
         ELSE
          FWH=FWH+DH2O
          IF(FWH .LT. 0.) THEN
           TWH=TWH+FWH
           FWH=0.
          ENDIF
         ENDIF
        ELSE
C  UNFROZEN WATER INCREASE (THAWING)                      
         IF(TWH .LT. 0.) THEN
          TWH=TWH+DH2O
         ELSE
          IF(SWH .GT. 1E-04) THEN
           ALP=DH2O*TWH/SWH
           TWH=TWH+ALP
           FWH=FWH+DH2O-ALP
           IF(FWH .GT. FWC) THEN
            TWH=TWH+FWH-FWC
            FWH=FWC
           ENDIF
          ELSE
           TWH=TWH+DH2O
          ENDIF
         ENDIF
         IF(TWH .GT. TWC) THEN
          FWH=FWH+TWH-TWC
          TWH=TWC
         ENDIF
        ENDIF
       ENDIF        
      endif
      
C  CHECK CONSISTENCY OF THE WATER BALANCE BETWEEN SAC-SMA AND
C  FROZEN GROUND STATES 
      S=TWC+FWC+F1WC
cvk 03/08      IF(ABS(STOT-S) .GT. 0.1) then
      IF(ABS(STOT-S) .GT. 0.5) then
       WRITE(iout,*)
     + ' ** WARN ** NO TOTAL WATER BALANCE:',NUP,STOT,TWC,FWC,F1WC
       iexit=-1
      endif       
      S=TWH+FWH+F1WH
cvk 03/08      IF(ABS(SLIQ-S) .GT. 0.1) then
      IF(ABS(SLIQ-S) .GT. 0.5) then
       WRITE(iout,*)
     + ' ** WARN ** NO LIQUID WATER BALANCE:',NUP,SLIQ,TWH,FWH,F1WH
       iexit=-1
      endif 

      RETURN
      END
            
